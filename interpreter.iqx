
** Zadanie dla lewopółkulowego interpretatora ** 
Skrót działania: Z puli zdań losowane jest jedno(bez zwracania), z puli słów dwa słowa(również bez zwracania).
Wyświetlane jest najpierw zdanie, potem punkt fiksacji (+), potem dwa słowa oraz "+".
Jeśli odpowiedź nie została udzielona podczas prezentacji słów, to pojawia się jeszcze biały, pusty ekran, podczas którego można udzielić odpowiedzi.
Czasy prezentacji można ustawiać osobno dla każdej z prezentowanych treści.
Zapisywane są póki co dane surowe oraz krótkie podsumowanie


******************************************* KONFIGURACJA *******************************************

** Ustawienia domyślne (czcionka, kolor ekranu, rozmiar ekranu)
<defaults>
/ fontstyle = ("Tahoma", 2.7%, false, false, false, false, 5, 1)
/ txcolor = (0, 0, 0)
/ screencolor = white
/ canvassize = (900px, 600px)
</defaults>


** Zmienne do modyfikacji:
------------Pozycje bodźców na ekranie ------------------ w procentach wielkości ekranu, można w pikselach
/left_xpos - pozycja lewego słowa na ekranie - w poziomie  
/left_ypos - pozycja lewego słowa na ekranie - w pionie 
analogicznie z prawym słowem (right_*pos) i zdaniem (sentence_*pos) 

----------- Czas wyświetlania elementów i przerwy -------  ? w ms czy lepiej w ramkach, by wyliczyć dokładny czas prezentacji ?
/sentenceduration - czas wyświetlania zdania przed słowami
/fixationduration - czas wyświetlania samego punktu fiksacji
/targetduration   - czas wyświetlania punktu fiksacji + słów
? jeśli ma być losowany czas wyswietlania czegokolwiek to albo zrobić counter, albo użyć expressions 

----------- Klawisze odpowiedzi -------------------------
/responsekey_left  - klawisz odpowiedzi do lewej strony (30=A, 38=L)
/responsekey_right - klawisz odpowiedzi przypisany do prawej strony (z.help)
/response_left_letter - to co w responsekey_left tylko zapisane jako litera; right analogicznie

-------------------------------------
Oprócz tu modyfikowanych zmiennych, ważne zmienne modyfikowane niżej w kodzie to
- liczba triali w bloku eksperymentalnym (w elementach "< block interpreterX >" )
- listy zdań i rzeczowników (przy rzeczownikach należy osobno poniżej wpisać ich liczbę)

<values> 
/ left_xpos=43
/ right_xpos=57
/ left_ypos=50
/ right_ypos=50
/ sentence_xpos=50
/ sentence_ypos=50

/ sentenceduration = 2000
/ fixationduration = 800
/ targetduration = 150
/ trening_feedback_duration = 2000
/ trening_remind_delay = 5000
/ trening_posttrialpause = 1000

/ responsekey_left = 33
/ responsekey_right = 36
/ response_left_letter="F"
/ response_right_letter="J"
</values> 


** Zmienne do zapisu danych, uaktualniane w trakcie 
/sum_total     - ile prób
/sum_right     - ile wybranych z prawej strony
/percent_right - w ilu % z wszystkich prób odpowiedź dot. prawej strony (=prawa/próby)
/percent_left
<values>
/sum_total=0
/sum_right=0
/percent_right=0
/percent_left=0

/total_latency=0
/final_answer=0
</values>


**** Zapis zmiennych do pliku - dane surowe 
data, czas, osoba, 
czas fiksacji, zdania, słów, czekania na odp po słowach
zdanie, słowo z lewej, słowo z prawej 
kod odpowiedzi dla prawej, kod odpowiedzi osoby badanej 
latencja
<data>
/file = "Interpreter_rawdata.iqdat"
/separatefiles = false
/columns = [date, time, subject, 
radiobuttons.q1.selectedvalue, radiobuttons.q2.selectedvalue, radiobuttons.q3.selectedvalue, 
radiobuttons.q4.selectedvalue, radiobuttons.q5.selectedvalue, radiobuttons.q6.selectedvalue, 
radiobuttons.q7.selectedvalue, radiobuttons.q8.selectedvalue, radiobuttons.q9.selectedvalue, 
radiobuttons.q10.selectedvalue, expressions.oldfield_suma,
values.fixationduration, values.sentenceduration, values.targetduration,
text.sentence.currentitem, text.lefttarget.currentitem, text.righttarget.currentitem,
trial.target.response, trial.blank.response, values.final_answer, trial.target.latency, trial.blank.latency, values.total_latency]
</data>


** Zapis zmiennych - podsumowanie 
data, czas, osoba badana -id,
czas fiksacji, czas pokazania slow, czas czekania na odp
inne czasy 

<expressions>
/ oldfield_suma = (radiobuttons.q1.selectedvalue + radiobuttons.q2.selectedvalue + 
radiobuttons.q3.selectedvalue + radiobuttons.q4.selectedvalue + radiobuttons.q5.selectedvalue +
radiobuttons.q6.selectedvalue + radiobuttons.q7.selectedvalue + radiobuttons.q8.selectedvalue +
radiobuttons.q9.selectedvalue + radiobuttons.q10.selectedvalue)
</expressions>

<summarydata>
/file = "Interpreter_summary.iqdat"
/columns = [script.startdate, script.starttime, script.subjectid,
expressions.oldfield_suma,
values.fixationduration, values.targetduration,
values.response_left_letter, values.response_right_letter, 
values.sum_total, values.sum_right,
values.percent_right, values.percent_left,
dropdown.select_student.selectedvalue ]
</summarydata>


******************************************* EKSPERYMENT *******************************************


<expt>
/preinstructions = (welcome)
/postinstructions = ()
/blocks = [1 = oldfield; 2 = interpreter_trening; 3 = interpreter1; 4-6 = interpreter2do4; 7 = podsumowanie]
</expt>


------------------------------------------- INSTRUKCJE -------------------------------------------

<instruct>
/windowsize = (100%, 100%)
/nextkey = (57)
/nextlabel = ("Spacja")
/prevlabel = ("Backspace")
/prevkey = (14)
/lastlabel = ("Spacja")
</instruct>


<page welcome>
^Dziękujemy, że zdecydowałaś/eś się wziąć udział w naszym badaniu!
^Dotyczy ono mechanizmów powstawania skojarzeń.
^Twój udział jest anonimowy, nie gromadzimy danych osobowych.

^^Badanie składa się z trzech części:
^1) Kwestionariusza preferencji stronnych,
^2) Sesji treningowej,
^3) Sesji testowej.

^^Całość badania nie potrwa dłużej niż 10 minut.
^Na końcu będziesz mieć możliwość zapoznania się z uzyskanymi wynikami.

^^^Gdy będziesz gotowy/a, naciśnij spację i przejdziemy do części pierwszej.
</page> 

<page instructions> 
^Teraz nastąpi właściwa część badania.

^^^W każdej próbie najpierw pojawi się przed tobą zdanie skonstruowane wg schematu KTOŚ-ZROBIŁ-COŚ. 
^Przeczytaj je uważnie i pomyśl czym ów ktoś mógł się kierować.
^Gdy zdanie zniknie, skup wzrok na "+" na środku ekranu. 
^Po chwili po obu stronach "+" migną krótko dwa słowa. 
^Zdecyduj, które z nich może być prawdopodobnie związane z motywem, którym kierowała się osoba opisana w zdaniu.
^Jeśli będzie to słowo po lewej stronie, naciśnij klawisz <%values.response_left_letter%>, a jeśli po prawej, wybierz klawisz <%values.response_right_letter%>.
^Jeśli oba pasują, wybierz to, które pasuje lepiej. 

^^Słowa dobierane są losowo, nie ma z góry ustalonych dobrych i złych odpowiedzi. Wybieraj kierując się własnym wrażeniem.

^^Uwaga: 
^Nie czytaj słów tak jak zwykle, przenosząc wzrok z jednego na drugie. W tym badaniu musisz utrzymywać wzrok skupiony na samym środku ekranu, w miejscu zaznaczonym
krzyżykiem. Dzięki temu kiedy tylko słowa się pojawią będziesz mógł/mogła odczytać oba jednocześnie.

^Słowa będą wyświetlane bardzo krótko, tak że ich dokładne odczytanie może być trudne. To normalne. 
Jeśli nie uda Ci się wyraźnie odczytać słowa, to nie szkodzi - zdaj się na to, co podpowiada Ci intuicja.
Jeśli i ona nie pomoże, zgaduj.
</page> 

<page trening> 
^Rozpocznijmy od sesji treningowej - ma ona na celu upewnienie się, że procedura jest jasna. Jej wynik nie zostanie zapisany.
^^Połóż palec wskazujący lewej ręki na klawiszu <%values.response_left_letter%>, a palec wskazujący prawej ręki na klawiszu <%values.response_right_letter%>.
^^Gdy będziesz gotowy/a, naciśnij spację. </page> 

<page wlasciwa_sesja> 
^Teraz rozpocznie się właściwa sesja badania.
^^Połóż palec wskazujący lewej ręki na klawiszu <%values.response_left_letter%>, a palec wskazujący prawej ręki na klawiszu <%values.response_right_letter%>.
^^Gdy będziesz gotowy/a, naciśnij spację. </page> 

<page przerwa> 
^^^Ukończone próby: <%values.sum_total%>, pozostało jeszcze: <%values.trials_count-values.sum_total%>. Czas na chwilę przerwy.
^^Gdy odpoczniesz, połóż ponownie palec wskazujący lewej ręki na klawiszu <%values.response_left_letter%>, a palec wskazujący prawej ręki na klawiszu <%values.response_right_letter%> i naciśnij spację.
</page>

------------------------------------------- PODSUMOWANIE -------------------------------------------

** strona nieużywana - zamieniona na survey podsumowanie
<page the_end> 
^^^To już koniec badania. Dziękujemy za udział!
^^^^Badanie miało na celu sprawdzenie hipotezy zakładającej, że w związku z różnicami sposobu w jaki lewa i prawa półkula mózgu przetwarza informacje językowe słowa
po lewej stronie będzie łatwiej skojarzyć z opisanymi sytuacjami.
^^Ty wybrałeś/aś <%values.sum_total-values.sum_right%> słów po lewej i <%values.sum_right%> słów po prawej stronie.
</page>

<survey podsumowanie>
/ pages = [1=final_page]
/ itemspacing = 2.5%
/ showpagenumbers = false
/ showquestionnumbers = false
/ showbackbutton = false
/ finishlabel = "Koniec"
/ recorddata = false
</survey>

<surveypage final_page>
/ caption = "To już koniec badania. Dziękujemy za udział!


Badanie miało na celu sprawdzenie hipotezy zakładającej, że w związku z różnicami sposobu w jaki lewa i prawa półkula mózgu przetwarza informacje językowe słowa po lewej stronie będzie łatwiej skojarzyć z opisanymi sytuacjami.

Ty wybrałeś/aś <%values.sum_total-values.sum_right%> słów po lewej i <%values.sum_right%> słów po prawej stronie."
/ questions = [1=select_student]
/ recorddata = false
/ nextbuttonposition = (50,90)
</surveypage>

<dropdown select_student>
/ caption = "Na zakończenie prosimy jeszcze, abyś wskazał/a osobę, która zaprosiła Cię do udziału w tym badaniu:"
/ options = ("Katarzyna Dudek", "Kinga Dudzik", "Jan Giza", "Helena Greber", "Monika Halicka", "Iwona Kopeć",
"Weronika Kot", "Krzysztof Kutt", "Mateusz Młynarczyk", "Anna Nowak", "Joanna Nowak", "Maciej Szul", "Maciej Ścibor",
"Anna Tereszko", "Anna Wasik", "Katarzyna Wiernasz", "Paulina Wróbel", "Agata Zimirska")
/ position = (10,60)
</dropdown>

*******************************************  OLDFIELD  *******************************************

<survey oldfield>
/ pages = [1=p1,p2]
/ itemspacing = 2.5%
/ showpagenumbers = false
/ showquestionnumbers = false
/ showbackbutton = false
/ nextlabel = "Dalej"
/ finishlabel = "Dalej"
/ recorddata = false
</survey>


<surveypage p1>
/ caption = "Przy każdej z wymienionych czynności i przedmiotów zaznacz której ręki wolisz używać"
/ questions = [1=q1, q2, q3, q4, q5]
/ recorddata = false
/ nextbuttonposition = (90,90)
</surveypage>

<surveypage p2>
/ caption = "Przy każdej z wymienionych czynności i przedmiotów zaznacz której ręki wolisz używać"
/ questions = [1=q6, q7, q8, q9, q10]
/ recorddata = false
/ nextbuttonposition = (90,90)
</surveypage>


<radiobuttons q1>
/ caption = "Pisanie"
/ options = ("Zawsze lewej", "Zazwyczaj lewej", "Wszystko jedno", "Zazwyczaj prawej", "Zawsze prawej")
/ optionvalues = ("-2", "-1", "0", "1", "2")
/ orientation = horizontal
/ position = (10,25)
</radiobuttons>

<radiobuttons q2>
/ caption = "Rysowanie"
/ options = ("Zawsze lewej", "Zazwyczaj lewej", "Wszystko jedno", "Zazwyczaj prawej", "Zawsze prawej")
/ optionvalues = ("-2", "-1", "0", "1", "2")
/ orientation = horizontal
/ position = (10,37)
</radiobuttons>

<radiobuttons q3>
/ caption = "Rzucanie"
/ options = ("Zawsze lewej", "Zazwyczaj lewej", "Wszystko jedno", "Zazwyczaj prawej", "Zawsze prawej")
/ optionvalues = ("-2", "-1", "0", "1", "2")
/ orientation = horizontal
/ position = (10,49)
</radiobuttons>

<radiobuttons q4>
/ caption = "Nożyczki"
/ options = ("Zawsze lewej", "Zazwyczaj lewej", "Wszystko jedno", "Zazwyczaj prawej", "Zawsze prawej")
/ optionvalues = ("-2", "-1", "0", "1", "2")
/ orientation = horizontal
/ position = (10,61)
</radiobuttons>

<radiobuttons q5>
/ caption = "Szczoteczka do zębów"
/ options = ("Zawsze lewej", "Zazwyczaj lewej", "Wszystko jedno", "Zazwyczaj prawej", "Zawsze prawej")
/ optionvalues = ("-2", "-1", "0", "1", "2")
/ orientation = horizontal
/ position = (10,73)
</radiobuttons>


<radiobuttons q6>
/ caption = "Nóż (bez widelca)"
/ options = ("Zawsze lewej", "Zazwyczaj lewej", "Wszystko jedno", "Zazwyczaj prawej", "Zawsze prawej")
/ optionvalues = ("-2", "-1", "0", "1", "2")
/ orientation = horizontal
/ position = (10,25)
</radiobuttons>

<radiobuttons q7>
/ caption = "Łyżka"
/ options = ("Zawsze lewej", "Zazwyczaj lewej", "Wszystko jedno", "Zazwyczaj prawej", "Zawsze prawej")
/ optionvalues = ("-2", "-1", "0", "1", "2")
/ orientation = horizontal
/ position = (10,37)
</radiobuttons>

<radiobuttons q8>
/ caption = "Miotła na kiju (ręka u góry)"
/ options = ("Zawsze lewej", "Zazwyczaj lewej", "Wszystko jedno", "Zazwyczaj prawej", "Zawsze prawej")
/ optionvalues = ("-2", "-1", "0", "1", "2")
/ orientation = horizontal
/ position = (10,49)
</radiobuttons>

<radiobuttons q9>
/ caption = "Zapalanie zapałki (zapałka)"
/ options = ("Zawsze lewej", "Zazwyczaj lewej", "Wszystko jedno", "Zazwyczaj prawej", "Zawsze prawej")
/ optionvalues = ("-2", "-1", "0", "1", "2")
/ orientation = horizontal
/ position = (10,61)
</radiobuttons>

<radiobuttons q10>
/ caption = "Otwieranie pudełka (pokrywka)"
/ options = ("Zawsze lewej", "Zazwyczaj lewej", "Wszystko jedno", "Zazwyczaj prawej", "Zawsze prawej")
/ optionvalues = ("-2", "-1", "0", "1", "2")
/ orientation = horizontal
/ position = (10,73)
</radiobuttons>


******************************************* INTERPRETER *******************************************


** TODO: /trials = [1-6 = sentence]
<block interpreter_trening>   
/preinstructions = (instructions, trening)
/trials = [1-6 = sentence_trening]
</block>


** ilość prób we właściwym badaniu (bez treningu)
<values>
/trials_count = 64
</values>


** TODO: /trials = [1-16 = sentence]
<block interpreter1>   
/preinstructions = (wlasciwa_sesja)
/trials = [1-16 = sentence]
/onblockend = [ values.percent_right=values.sum_right/values.sum_total*100 ]
/onblockend = [ values.percent_left=(values.sum_total-values.sum_right)/ values.sum_total*100 ]
</block>


** TODO: /trials = [1-16 = sentence]
<block interpreter2do4>   
/preinstructions = (przerwa)
/trials = [1-16 = sentence]
/onblockend = [ values.percent_right=values.sum_right/values.sum_total*100 ]
/onblockend = [ values.percent_left=(values.sum_total-values.sum_right)/ values.sum_total*100 ]
</block>


------------------------------------------- TRIALE -------------------------------------------


****** Próby: 
- trial.sentence wywołuje trial fixation, który wywołuje trial target, który (jesli podczas niego nie zostala podana odpowiedz) wywola trial blank
 (możliwość manipulowania czasami wyświetlania odrębnie) 

** Próba(ramka) ze zdaniem 
<trial sentence>
/stimulusframes = [1 = blank, sentence]
/timeout = values.sentenceduration
/branch = [trial.fixation]
/recorddata = false
</trial>

** Próba (ramka) z punktem fiksacji
<trial fixation>
/stimulusframes = [1 = fixation]
/timeout = values.fixationduration
/branch = [trial.target]
/recorddata = false
</trial>


** Próba (ramka) właściwa, z wyborem słowa   
tu możliwość odpowiedzi i wyliczane zmienne do podsumowania
<trial target>
/ontrialbegin = [trial.target.recorddata=true; 
                 values.final_answer=0;
				 values.total_latency=0]
/stimulusframes = [1 = fixation, lefttarget, righttarget]
/isvalidresponse = [trial.target.response == values.responsekey_left || trial.target.response == values.responsekey_right]
/timeout=values.targetduration
/iscorrectresponse = [ trial.target.response == values.responsekey_right ]
/ontrialend = [values.sum_total+=1;
               values.total_latency=trial.target.latency;
				if (trial.target.correct) {
            			values.sum_right+=1 }; 
				if (trial.target.response != values.responsekey_right  && trial.target.response != values.responsekey_left) 
						{trial.target.recorddata=false}
				else {values.final_answer=trial.target.response} ] 
/branch = [if (trial.target.response != values.responsekey_right  &&  trial.target.response != values.responsekey_left) { trial.blank}]
</trial>

**Próba (ramka) pusta pozwalająca na udzielenie odpowiedzi po zniknięciu słów
<trial blank>
/stimulusframes = [1 = blank]
/isvalidresponse = [trial.blank.response == values.responsekey_left || trial.blank.response == values.responsekey_right]
/iscorrectresponse = [ trial.blank.response == values.responsekey_right ]
/ontrialend = [if (trial.blank.correct) {
				     values.sum_right+=1 };
				values.final_answer=trial.blank.response;
				values.total_latency+=trial.blank.latency]
</trial>


------------------------------------------- TRIALE TRENINGOWE-------------------------------------------

** Próba(ramka) ze zdaniem 
<trial sentence_trening>
/stimulusframes = [1 = blank, sentence]
/timeout = values.sentenceduration
/branch = [trial.fixation_trening]
/recorddata = false
</trial>

** Próba (ramka) z punktem fiksacji
<trial fixation_trening>
/stimulusframes = [1 = fixation]
/timeout = values.fixationduration
/branch = [trial.target_trening]
/recorddata = false
</trial>

** Próba (ramka) właściwa, z wyborem słowa   
tu możliwość odpowiedzi i wyliczane zmienne do podsumowania 
<trial target_trening>
/ontrialbegin = [trial.target_trening.recorddata=false]
/stimulusframes = [1 = fixation, lefttarget, righttarget]
/isvalidresponse = [trial.target_trening.response == values.responsekey_left || trial.target_trening.response == values.responsekey_right]
/timeout=values.targetduration
/iscorrectresponse = [ trial.target_trening.response == values.responsekey_right ]
/branch = [if (trial.target_trening.response == values.responsekey_left) {trial.trening_left}]
/branch = [if (trial.target_trening.response == values.responsekey_right) {trial.trening_right}]
/branch = [{trial.blank_trening}]
/recorddata = false
</trial>

**Próba (ramka) pusta pozwalająca na udzielenie odpowiedzi po zniknięciu słów
<trial blank_trening>
/stimulusframes = [1 = blank]
/isvalidresponse = [trial.blank_trening.response == values.responsekey_left || trial.blank_trening.response == values.responsekey_right]
/iscorrectresponse = [ trial.blank_trening.response == values.responsekey_right ]
/branch = [if (trial.blank_trening.response == values.responsekey_right) {trial.trening_right}]
/branch = [if (trial.blank_trening.response == values.responsekey_left) {trial.trening_left}]
/branch = [{trial.remind_trening}]
/timeout=values.trening_remind_delay
/recorddata = false
</trial>

<trial remind_trening>
/stimulusframes = [1 = trening_reminder]
/isvalidresponse = [trial.remind_trening.response == values.responsekey_left || trial.remind_trening.response == values.responsekey_right]
/iscorrectresponse = [ trial.remind_trening.response == values.responsekey_right ]
/branch = [if (trial.remind_trening.response == values.responsekey_right) {trial.trening_right}]
/branch = [if (trial.remind_trening.response == values.responsekey_left) {trial.trening_left}]
/recorddata = false
</trial>


<trial trening_right>
/stimulusframes = [1 = trening_right]
/timeout = values.trening_feedback_duration
/recorddata = false
/posttrialpause = values.trening_posttrialpause
</trial>

<trial trening_left>
/stimulusframes = [1 = trening_left]
/timeout = values.trening_feedback_duration
/recorddata = false
/posttrialpause = values.trening_posttrialpause
</trial>



------------------------------------------- ELEMENTY PODSTAWOWE -------------------------------------------


** /select=noreplace(1-X)  --  X=ilość zdań/słów w puli
** /resetinterval = 1000  --  po ilu blokach resetuje się pula (u nas jest tylko kilka bloków, więc liczba 100 gwarantuje,
                             że u nas nie nastąpi reset

<counter random_sentences_counter>
/allowrepeats = false
/select=noreplace(1-70)
/selectionrate = always
/resetinterval = 1000
</counter>

<counter random_nouns_counter>
/allowrepeats = false
/select=noreplace(1-103)
/selectionrate = always
/resetinterval = 1000
</counter>

<text sentence> 
/items=sentences
/select = random_sentences_counter
/hposition = values.sentence_xpos
/vposition = values.sentence_ypos
</text>

<text lefttarget>
/items = nouns
/select = random_nouns_counter
/hposition = values.left_xpos
/vposition = values.left_ypos
</text> 

<text righttarget> 
/items=nouns
/select= random_nouns_counter
/hposition = values.right_xpos
/vposition = values.right_ypos
</text> 


<text fixation>
/items=("+")
/position = (50,50)
</text>

** Biały prostokąt zakrywający ekran 
<shape blank>
/color=white
/shape=rectangle
/position=(0%,0%)
/size = (100%,100%) 
/valign = top
/halign = left
</shape>


<text trening_reminder> 
/items=("Proszę, wybierz odpowiedź.

Przypominamy, że palec wskazujący lewej ręki powinien leżeć na klawiszu <%values.response_left_letter%>, a palec wskazujący prawej ręki na klawiszu <%values.response_right_letter%>.")
</text>

<text trening_right> 
/items=("Dobrze. Wybrałaś/eś słowo z prawej strony.")
</text>

<text trening_left> 
/items=("Dobrze. Wybrałaś/eś słowo z lewej strony.")
</text>

------------------------------------------- PULA ITEMÓW -------------------------------------------

** Pula zdań do losowania 
<item sentences>
/1="Sąsiedzi przygarnęli kota"
/2="Nauczyciel kupił kubek"
/3="Portier postanowił rzucić pracę"
/4="Sprzedawca kupił akcje"
/5="Lekarz wyjechał do Afryki"
/6="Andrzej poszedł do kina"
/7="Jerzy kupił rower"
/8="Prezes zatrudnił pomocnika"
/9="Muzyk sprzedał klarnet"
/10="Dziewczynka zgubiła lalkę"
/11="Złodziej ukradł klucze"
/12="Lekarz uratował dziecko"
/13="Piotr sięgnął po klucz"
/14="Uczennica starła tablice"
/15="Pracownik został w biurze"
/16="Wokalista wygrał konkurs"
/17="Joanna wróciła z podróży"
/18="Dyrektor wyjechał na urlop"
/19="Sprzedawca otworzył sklep"
/20="Fryzjer wybrał się na szkolenie"
/21="Anna zgłosiła kandydaturę"
/22="Siostra wróciła do domu"
/23="Brat wypuścił psa"
/24="Sąsiad jeździł na rolkach"
/25="Laborant odkrył wzór"
/26="Leń postanowił pracować"
/27="Właściciel sprzedał dom"
/28="Aptekarka wzięła receptę"
/29="Ola zamieściła ogłoszenie"
/30="Redaktor wystąpił w programie"
/31="Polityk obiecał inwestycję"
/32="Kierowca wyprzedził pieszych"
/33="Strażak uratował dzieci"
/34="Ambasador urządził przyjęcie"
/35="Pisarz postanowił wznowić pracę"
/36="Marcin urządził przyjęcie"
/37="Robotnik przełożył pracę"
/38="Kaśka wyjechała na wakacje"
/39="Michał wybrał szparagi"
/40="Jan przygotował pracę"
/41="Wujek zabrał kanapki"
/42="Przechodzień zamówił kawę"
/43="Fotograf zabrał aparat"
/44="Kasjer zabrał paczkę"
/45="Jan przełączył zasilanie"
/46="Okulista zjadł obiad"
/47="Prawnik sprawdził wiadomości"
/48="Jurek wyjechał na ryby"
/49="Wiktor kupił buty"
/50="Malarz skończył projekt"
/51="Mirek wypłynął w rejs"
/52="Ania zamieniła książki"
/53="Administrator założył wątek"
/54="Piłkarz podbiegł do piłki"
/55="Ornitolog wybrał lornetkę"
/56="Kibic wybrał się na mecz"
/57="Drwal wybrał piłę"
/58="Przyjaciele wybrali się na koncert"
/59="Fani podbiegli do zespołu"
/60="Lekarze podjęli decyzję"
/61="Stolarze podeszli do drzwi"
/62="Znajomy wybrał się do lasu"
/63="Kobieta zabrała klucze"
/64="Sędzia ogłosił przerwę"
/65="Zwycięzca wbiegł na podium"
/66="Nurek podszedł do liny"
/67="Szewc rozpoczął pracę"
/68="Kucharz zabrał przyprawy"
/69="Justyna znalazła mieszkanie"
/70="Ratownik wbiegł na dach"
</item>


** Pula rzeczowników do losowania
<item nouns> 
/1="brat"
/2="broń"
/3="brwi"
/4="brzeg"
/5="buty"
/6="chłopak"
/7="chwila"
/8="ciemność"
/9="cień"
/10="cisza"
/11="deszcz"
/12="dłoń"
/13="doktor"
/14="droga"
/15="dziecko"
/16="dziewczyna"
/17="dźwięk"
/18="fakt"
/19="głowa"
/20="historia"
/21="kamień"
/22="kapitan"
/23="kawałek"
/24="koło"
/25="krew"
/26="krok"
/27="król"
/28="krzyk"
/29="książę"
/30="lekarz"
/31="liście"
/32="miasto"
/33="miecz"
/34="miłość"
/35="moment"
/36="morze"
/37="niebo"
/38="obraz"
/39="oddech"
/40="oficer"
/41="ogień"
/42="ogródek"
/43="okno"
/44="pamięć"
/45="panna"
/46="pies"
/47="plecy"
/48="podróż"
/49="pokój"
/50="pole"
/51="południe"
/52="pomoc"
/53="pomysł"
/54="postać"
/55="powietrze"
/56="północ"
/57="praca"
/58="prawo"
/59="problem"
/60="profesor"
/61="przyjaciel"
/62="punkt"
/63="pytanie"
/64="radość"
/65="ramię"
/66="ręka"
/67="rodzina"
/68="rozkaz"
/69="rozmowa"
/70="samochód"
/71="serce"
/72="słońce"
/73="słowo"
/74="spojrzenie"
/75="spokój"
/76="sprawa"
/77="statek"
/78="stół"
/79="strach"
/80="ślady"
/81="światło"
/82="telefon"
/83="tłum"
/84="tydzień"
/85="umysł"
/86="usta"
/87="uśmiech"
/88="wiatr"
/89="widok"
/90="wieczór"
/91="wino"
/92="woda"
/93="wojna"
/94="wypadek"
/95="wzrok"
/96="zachód"
/97="zadanie"
/98="zapach"
/99="zęby"
/100="ziemia"
/101="znak"
/102="zwierzę"
/103="żona"
</item>

